/*
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'idea'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8
archivesBaseName = 'pravega-stream-status-tool'

plugins.withId('checkstyle') {
    checkstyle {
        toolVersion = checkstyleToolVersion

        configFile = new File(rootDir, "checkstyle/checkstyle.xml")
        configProperties = [importControlFile: "$rootDir/checkstyle/import-control.xml"]
    }

    plugins.withId('java') {
        checkstyle {
            checkstyleMain.exclude '**/rest/generated/**'
        }
    }
}

def withoutLog4j = { exclude group: 'org.slf4j', module: 'slf4j-log4j12' }

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile "io.pravega:pravega-common:${pravegaVersion}"
    compile "commons-cli:commons-cli:${commonCliVersion}"


    compile "org.slf4j:slf4j-api:1.7.14"
    compile "ch.qos.logback:logback-classic:1.1.7"

    compile group: 'org.apache.bookkeeper', name: 'bookkeeper-server', version: bookKeeperVersion, withoutLog4j
    compile group: 'org.apache.hadoop', name: 'hadoop-common', version:hadoopVersion, withoutLog4j
    compile group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion, withoutLog4j
}



tasks

task startStreamStat(type: JavaExec){
    main = "io.pravega.tools.pravegastreamstat.service.StreamStat"
    classpath = sourceSets.main.runtimeClasspath
}

task scriptStreamStat(type: CreateStartScripts) {
    outputDir = file('build/scripts')
    mainClassName = 'io.pravega.tools.pravegastreamstat.service.StreamStat'
    applicationName = 'StreamStat'
    defaultJvmOpts = ["-Dlogback.configurationFile=file:conf/logback.xml",
                      "-Dpravega.configurationFile=file:conf/config.properties"]
    classpath = files(jar.archivePath) + sourceSets.main.runtimeClasspath
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('bin') {
                from project.scriptStreamStat
            }
            into('lib') {
                from(jar)
                from(project.configurations.runtime)
            }
            into('conf') {
                from(project.file('src/main/resources/logback.xml'))
                from(project.file('src/main/resources/config.properties'))
            }
        }
    }
}